set(EXTENSION_NAME "ChromeDevTools")

set(SOURCES
    "Include/Babylon/Extensions/ChromeDevTools.h")

if (WIN32)
    set(CHROME_DEVTOOLS_SUPPORTED_ENGINES ${CHROME_DEVTOOLS_SUPPORTED_ENGINES} "JSI")
    if (NOT WINDOWS_STORE)
        set(CHROME_DEVTOOLS_SUPPORTED_ENGINES ${CHROME_DEVTOOLS_SUPPORTED_ENGINES} "V8")
    endif()
elseif(ANDROID)
    set(CHROME_DEVTOOLS_SUPPORTED_ENGINES ${CHROME_DEVTOOLS_SUPPORTED_ENGINES} "V8")
endif()

if (NAPI_JAVASCRIPT_ENGINE IN_LIST CHROME_DEVTOOLS_SUPPORTED_ENGINES)
    set(SOURCES ${SOURCES} "Source/ChromeDevTools${NAPI_JAVASCRIPT_ENGINE}.cpp")
else()
    set(SOURCES ${SOURCES} "Source/ChromeDevToolsNull.cpp")
endif()

add_library(${EXTENSION_NAME} ${SOURCES})
warnings_as_errors(${EXTENSION_NAME})

if (NAPI_JAVASCRIPT_ENGINE IN_LIST CHROME_DEVTOOLS_SUPPORTED_ENGINES AND NAPI_JAVASCRIPT_ENGINE STREQUAL "V8")
    target_link_libraries(${EXTENSION_NAME} PRIVATE v8inspector)
endif()

target_include_directories(${EXTENSION_NAME} PRIVATE "Include/Babylon/Extensions")
target_include_directories(${EXTENSION_NAME}
    PUBLIC "Include")

target_link_libraries(${EXTENSION_NAME}
    PUBLIC napi
    PRIVATE JsRuntime)

set(BABYLON_NATIVE_PLAYGROUND_EXTENSION_LIBRARIES
        ${BABYLON_NATIVE_PLAYGROUND_EXTENSION_LIBRARIES}
        PRIVATE ${EXTENSION_NAME}
        CACHE INTERNAL "")

set_property(TARGET ChromeDevTools PROPERTY FOLDER Extensions)
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES ${SOURCES})
